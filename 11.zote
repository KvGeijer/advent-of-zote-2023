include!("stdlib");
include!("aoc.zote");

input := read("input");

lines := input >> split("\n");

srows := set();
scols := set();
for (row, line) in lines >> enumerate {
	for (col, char) in line >> enumerate {
		if char == "#" {
			row >> insert(srows);
			col >> insert(scols);
		}
	}
}

for skip_len in [2, 1000000] >> map(\>> sub(1)) {
	karta := set();

	skipped_rows := 0;
	for (row, line) in lines >> enumerate {
		skipped_cols := 0;
		if row >> in(srows) >> not {
			skipped_rows += skip_len;
		} else {
			for (col, char) in line >> enumerate {
				if col >> in(scols) >> not {
					skipped_cols += skip_len;
				} else if char == "#" {
					[row + skipped_rows, col+skipped_cols] >> insert(karta);
				}
			}
		}
	}
	karta = karta >> keys;

	dists := 0;
	for (x1, y1) in karta for (x2, y2) in karta dists += abs(x1 - x2) + abs(y1 - y2);
	dists/2 >> print;
}
